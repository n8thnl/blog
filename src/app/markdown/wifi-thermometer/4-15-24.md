## Testing some Hardware

##### 4-15-2024
  

The biggest design choice in this project is going to be the microcontroller and wifi transmission components. Originally, since I'm a PIC fan I was going to use one of the PIC chips with an ADC (something like a PIC16Fxxx), but since I've been living under a rock, I was able to stumble upon the ESP32, an up-and-coming microcontroller not many people are talking about \\s.

  

I ordered a few thermistors from Digikey and wanted to set up a barebones temperature reading circuit to dip my feet in the water. One was a PTC thermistor, and to be honest, I haven't even touched that one. The reason being that the other two are 10k NTC thermistors which seem to be much nicer to work with (math-wise). Don't quote me on that, though.

  

The two 10k thermistors I'll be trying out:

*   [Littelfuse glass encapsulated 10k](https://www.digikey.com/en/products/detail/littelfuse-inc/103FG1K/4027447)
*   [Murata bead 10k](https://www.digikey.com/en/products/detail/murata-electronics/NXFT15XH103FA1B025/5797781)

  

The prices of these thermistors are in the $0.50-$0.90 range, and using an SMD thermistor will cost significantly less ($0.10), but I'm really digging the look of the glass thermistor. I think that making it a part of the design in general would make it a bit unique as well. We'll park that, though.

  

I'm wanting to see the characteristics of each and ultimately which type of thermistor would be more conducive to an ambient temperature sensor. The factors I'm looking for are:

*   accuracy
*   stability
*   consistency

  

Unfortunately, we're not going to be able to capture an accuracy metric or do any calibration to these thermistors since the reference thermometers I'm finding online are ~$200 USD which really goes against the budget project mindset.

  

### The Circuit

The test circuit is extremely simple, I've got 2 voltage dividers where we can test the resistance of each thermistor. I chose a 7500k resistor for each since the resistance of the thermistor is likely going to be around the 9k-11k ohm range. Originally I had this circuit running on an Arduino and the ADC resolution forced me to use a larger and larger resistor to register any change in voltage. I could probably bump this up to a 10k+ resistor, so I'll put that as a next step. These two dividers are fed into two separate ADC inputs on my [ESP32 dev board](https://www.digikey.com/en/products/detail/espressif-systems/ESP32-PICO-KIT/9381703).

  

![circuit-frizting.png](/wifi-thermometer/circuit-fritzing.png)

  

Once we have the voltage, we can easily find the resistance by using the voltage divider equation solving for R2:

<pre><code class="language-clike">R2 = R1 * (Vin / Vout - 1)</code></pre>

Once we have the resistance, we need the specific _B-constant_ for each thermistor. This will be in each datasheet. For our glass thermistor, this is `3892K` and our bead thermistor is `3380K` . The resistance along with the _B-constants_ are then fed into this formula:

<pre><code class="language-clike">temp_kelvin = B / (ln(resistance / 10000.0 * exp(-1 * B / (273.15 + 25))))</code></pre>

Ok, let's break this down. The resistance/temperature relationship that these 10k NTC thermistors have is an inverse logarithmic curve. The `10000.0` constant along with the `273.15 + 25` state that the resistance of this NTC thermistor is `298.15K` (kelvin) at a `10k` reference resistance. These two values are the axis references. Note that the output temperature is in Kelvin, so we do need to clean that up for a more readable output.

### Code

<pre class="line-numbers"><code class="language-clike">const int ADC_PIN_GLASS = 34;
const int ADC_PIN_BEAD = 12;

float KNOWN_RESISTANCE = 7500;
float TEMP_REFERENCE = 25 + 273.15;

int B_GLASS = 3892;
int B_BEAD = 3380;

int global_count = 0;

float v_at_analog_pin(int pin_num) {
  int raw = analogRead(pin_num);
  return raw * (3.3 / 4095.0);
}

float find_unknown_R_given_drop(float voltage_drop, int known_resistance) {
  return known_resistance * (3.3 / voltage_drop - 1);
}

float temp_K_from_R_10k(int B, float resistance) {
  return B / (log(resistance / (10000.0 * exp(-1 * B / TEMP_REFERENCE))));
}

float temp_K_to_C(float temp_K) {
  return temp_K - 273.15;
}

float temp_C_to_F(float temp_C) {
  return (temp_C * 9 / 5) + 32;
}

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);
  delay(1000);
}

void loop() {
  // put your main code here, to run repeatedly:
  float v_glass = v_at_analog_pin(ADC_PIN_GLASS);
  float v_bead  = v_at_analog_pin(ADC_PIN_BEAD);

  // Serial.println("v_glass: " + String(v_glass));
  // Serial.println("v_bead: " + String(v_bead));

  float r_glass = find_unknown_R_given_drop(v_glass, KNOWN_RESISTANCE);
  float r_bead  = find_unknown_R_given_drop(v_bead,  KNOWN_RESISTANCE);

  // Serial.println("r_glass: " + String(r_glass));
  // Serial.println("r_bead: " + String(r_bead));

  float temp_C_glass = temp_K_to_C(temp_K_from_R_10k(B_GLASS, r_glass));
  float temp_C_bead  = temp_K_to_C(temp_K_from_R_10k(B_BEAD,  r_bead));

  float temp_F_glass = temp_C_to_F(temp_C_glass);
  float temp_F_bead  = temp_C_to_F(temp_C_bead);

  Serial.println("glass temp F: " + String(temp_F_glass));
  Serial.println("bead temp F: " + String(temp_F_bead));
  Serial.println("");

  delay(500);
}</code></pre>

### Output

The above code run on the ESP32 as shown produces the following output:

![esp32-dual-thermistor-output.gif](/wifi-thermometer/esp32-dual-thermistor-output.gif)

  

This was just maybe 12 or so seconds of the output, but it seems that the signal from the glass thermistor was much less corrupted by noise. The bead was weirdly 2-3 degrees lower than the glass thermistor and when I checked with an infrared thermometer, the temperature that it read was almost dead on the glass thermistor. This makes me believe maybe I'm missing something with the bead thermistor, but it's a 10k thermistor just like the glass one and the B-constant I pulled was the `25/50` just as it was for the glass thermistor.

  

#### Next steps:

*   Get my hands on an SMD thermistor to test that against the glass
*   Try with different resistor values
*   Research charging circuits and batteries
*   actually charge a battery

